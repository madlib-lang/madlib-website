import { Header } from "Http"
import List from "List"
import { Just, Nothing } from "Maybe"
import Process from "Process"
import String from "String"

import Server from "MadServer"

import { postPlaygroundHandler } from "@server/Api"
import { fileHandler } from "@server/FileHandler"



BASE_API_PATH :: String
BASE_API_PATH = "/api"

DEFAULT_PORT :: Integer
DEFAULT_PORT = 3000


makeApiPath :: String -> String
makeApiPath = (path) => mconcat(BASE_API_PATH, path)


port :: Integer
port = pipe(
  List.nth(1),
  chain(scan),
  where {
    Just(p) =>
      p

    Nothing =>
      DEFAULT_PORT
  }
)(Process.Argv)


withSSLOptions :: Server.Options
withSSLOptions = pipe(
  map(Process.getEnv),
  where {
    [Just(certificateFile), Just(keyFile), caFile,] =>
      {
        ...Server.DEFAULT_OPTIONS,
        ssl: Just({ certificateFile, keyFile, caFile, passphrase: Nothing, }),
      }

    _ =>
      Server.DEFAULT_OPTIONS
  }
)(["SSL_CERT", "SSL_KEY", "SSL_CA",])


main :: List String -> {}
main = () => {
  pipe(
    Server.create,
    Server.post(makeApiPath("/playgrounds"), postPlaygroundHandler),
    Server.get("/*", fileHandler),
    Server.run(port)
  )({ ...withSSLOptions, verbose: true, })


  // TODO: make configurable
  if (withSSLOptions.ssl != Nothing) do {
    pipe(
      Server.create,
      Server.any(
        "/*",
        (req) => of({
          body: "",
          status: 301,
          headers: [Header("Location", `https://www.madlib.space${req.url}`),],
        })
      ),
      Server.run(80)
    )({ ssl: Nothing, verbose: true, })
  }
}
